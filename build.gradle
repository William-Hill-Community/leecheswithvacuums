group = 'au.com.williamhill.leecheswithvacuums'
version = '0.1.0-SNAPSHOT'
  
allprojects {
  apply plugin: 'java'
  apply plugin: 'jacoco'

  repositories {
    jcenter()
  }
  
  dependencies {}

  jacoco {
    toolVersion = '0.7.9'
  }

  jacocoTestReport {
    additionalSourceDirs = files(sourceSets.main.allSource.srcDirs)
    sourceDirectories = files(sourceSets.main.allSource.srcDirs)
    classDirectories = files(sourceSets.main.output)
    reports {
      html.enabled = true
      xml.enabled = true
      csv.enabled = false
    }
  }
}

subprojects {}

task jacocoRootReport(type: JacocoReport) {
  dependsOn = allprojects.test
  additionalSourceDirs = files(allprojects.sourceSets.main.allSource.srcDirs)
  sourceDirectories = files(allprojects.sourceSets.main.allSource.srcDirs)
  classDirectories = files(allprojects.sourceSets.main.output)
  executionData = files(allprojects.jacocoTestReport.executionData)
  reports {
    html.enabled = true
    xml.enabled = false
    csv.enabled = false
  }
  onlyIf = {
    true
  }
  doFirst {
    executionData = files(executionData.findAll {
      it.exists()
    })
  }
}

def packageName = 'leecheswithvacuums'

dependencies {
  compile 'au.com.williamhill.flywheel:flywheel-core:0.2.0'
  compile 'org.slf4j:slf4j-api:1.6.1'
  
  runtime 'org.slf4j:slf4j-log4j12:1.6.1'
  runtime 'log4j:log4j:1.2.17'
}

jar {
  baseName packageName
  finalizedBy jacocoRootReport
}

task fullJar(type: Jar) {
  dependsOn configurations.compile
  dependsOn configurations.runtime
  manifest {
    attributes 'Main-Class': 'au.com.williamhill.leecheswithvacuums.Leeches'
  }
  from sourceSets.main.output
  from { // bundle all dependencies
    configurations.compile.collect { it.isDirectory() ? it : zipTree(it) }
    configurations.runtime.collect { it.isDirectory() ? it : zipTree(it) }
  }   
  baseName packageName + '-full'
}

task javadocJar(type: Jar, dependsOn: javadoc) {
  baseName packageName
  classifier = 'javadoc'
  from "$buildDir/docs/javadoc"
}
 
task sourcesJar(type: Jar) {
  baseName packageName
  from sourceSets.main.allSource
  classifier = 'sources'
}

task leeches(type: JavaExec) {
  systemProperties = System.properties
  classpath = sourceSets.main.runtimeClasspath
  main = 'au.com.williamhill.leecheswithvacuums.Leeches'
}

task pump(type: JavaExec) {
  systemProperties = System.properties
  classpath = sourceSets.main.runtimeClasspath
  main = 'au.com.williamhill.leecheswithvacuums.Pump'
}

build {
  dependsOn fullJar
}
